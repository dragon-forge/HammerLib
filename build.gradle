buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.+"
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
    id 'net.neoforged.gradle' version '[6.0.13, 6.2)'
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'org.spongepowered.mixin'

ext.configFile = file("build.txt")
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse(prop)
    project.ext.hasConfig = { key ->
        return config.isSet(key);
    }
}

version = config.mod_version
group = config.maven_group
archivesBaseName = "${config.archive_name}-${config.mc_version}"

mixin {
    add sourceSets.main, "mixins.${config.mod_id}.refmap.json"
    config "mixins.${config.mod_id}.json"

    debug.verbose = true
    debug.export = true
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'official', version: config.mc_version
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    copyIdeResources = true
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', config.mod_id
            setArgs(Arrays.asList("--mixin mixins.${config.mod_id}.json --username ${config.mc_user.split(',')[0]} --uuid ${config.mc_user.split(',')[1]}".split(' ')))

            mods {
                "${config.mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            setArgs(Arrays.asList("--mixin mixins.${config.mod_id}.json".split(' ')))
            property 'forge.enabledGameTestNamespaces', config.mod_id
            mods {
                "${config.mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "Zeitheron Maven"
        url = "https://maven.zeith.org"
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/" // location of the maven that hosts JEI files since January 2023
    }
    maven { setUrl("https://oss.sonatype.org/content/repositories/public/") }
    flatDir { dirs "libraries/${config.mc_version}/all" }
    flatDir { dirs "libraries/${config.mc_version}/classpathOnly" }
    flatDir { dirs "libraries/${config.mc_version}/runtimeOnly" }
    flatDir { dirs "libraries/embedded" }

    maven {
        url "https://maven.firstdarkdev.xyz/snapshots"
        content {
            includeGroupByRegex "com\\.lowdragmc.*"
        }
    }
    maven {
        url = "https://jitpack.io"
        content {
            includeGroupByRegex "com\\.github\\.LlamaLad7.*"
        }
    }
}

import net.minecraftforge.gradle.common.tasks.SignJar

import java.nio.file.Files
import java.nio.file.StandardOpenOption

dependencies {
    minecraft "net.neoforged:forge:${config.mc_version}-${config.forge_version}"

    if (hasConfig("jei_version") && !config.jei_version.isEmpty()) {
        compileOnly fg.deobf("mezz.jei:jei-${config.mc_version}-common-api:${config.jei_version}")
        compileOnly fg.deobf("mezz.jei:jei-${config.mc_version}-forge-api:${config.jei_version}")
        runtimeOnly fg.deobf("mezz.jei:jei-${config.mc_version}-forge:${config.jei_version}")
    }

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    compileOnly fg.deobf("com.lowdragmc.shimmer:Shimmer-forge:1.20-${config.shimmer_version}")

    def libraries = new File(file("libraries"), "${config.mc_version}")
    if (!libraries.isDirectory()) libraries.mkdirs()

    def libDir = new File(libraries, "all");
    if (libDir.isDirectory()) {
        libDir.eachFile { file ->
            def splitPos = file.name.lastIndexOf("-")
            def modartifact = file.name.substring(0, splitPos)
            def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
            dependencies {
                implementation fg.deobf(project.dependencies.create("_all:$modartifact:$modversion") {
                    transitive = false
                })
            }
        }
    } else libDir.mkdir();

    libDir = new File(libraries, "classpathOnly");
    if (libDir.isDirectory()) {
        libDir.eachFile { file ->
            def splitPos = file.name.lastIndexOf("-")
            def modartifact = file.name.substring(0, splitPos)
            def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
            dependencies {
                compileOnly fg.deobf(project.dependencies.create("_classpath:$modartifact:$modversion") {
                    transitive = false
                })
            }
        }
    } else libDir.mkdir();

    libDir = new File(libraries, "runtimeOnly");
    if (libDir.isDirectory()) {
        libDir.eachFile { file ->
            def splitPos = file.name.lastIndexOf("-")
            def modartifact = file.name.substring(0, splitPos)
            def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
            dependencies {
                runtimeOnly fg.deobf(project.dependencies.create("_runtime:$modartifact:$modversion") {
                    transitive = false
                })
            }
        }
    } else libDir.mkdir();

    libDir = new File(file("libraries"), "embedded");
    if (libDir.isDirectory()) {
        libDir.eachFile { file ->
            def splitPos = file.name.lastIndexOf("-")
            def modartifact = file.name.substring(0, splitPos)
            def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
            def modreference = "lib:$modartifact:$modversion"

            dependencies {
                def dep = project.dependencies.create(modreference) {
                    transitive = false
                }
                implementation dep
                shadow dep
            }
        }
    } else libDir.mkdir();
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version: config.mc_version, minecraft_version_range: config.mc_range,
        forge_version: "${config.mc_version}-${config.forge_version}", forge_version_range: config.forge_range,
        fmljava_range: config.fmljava_range,
        mod_id: config.mod_id, mod_name: config.mod_name, mod_license: config.mod_license, mod_version: config.mod_version,
        issue_tracker: config.issue_tracker, modrinth_project: config.modrinth_project,
        mod_authors: config.mod_authors, mod_description: config.mod_description
]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : config.mod_id,
                "Specification-Vendor"    : config.mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : config.mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "mixins.${config.mod_id}.json"
        ])
    }
}

task deobfJar(type: Jar) {
    archiveClassifier = 'deobf'
    from sourceSets.main.output
    manifest {
        attributes([
                "Specification-Title"     : config.mod_id,
                "Specification-Vendor"    : config.mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : config.mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "mixins.${config.mod_id}.json"
        ])
    }
}

task srcJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}

modrinth {
    token = System.env.MODRINTH_TOKEN != null ? System.env.MODRINTH_TOKEN : "NONE"
    projectId = config.modrinth_project as String
    versionNumber = config.mod_version as String
    versionType = config.release_type as String
    uploadFile = jar
    gameVersions = [config.mc_version as String]
}

curseforge {
    apiKey = System.env.CURSEFORGE_API_KEY != null ? System.env.CURSEFORGE_API_KEY : "NONE"
    project {
        id = config.cf_project as String
        changelogType = 'html'
        changelog = ""
        releaseType = config.release_type as String
        addGameVersion config.mc_version as String
        addGameVersion "Forge"
        addGameVersion "NeoForge"
        mainArtifact(jar) {
            displayName = "[${config.mc_version}] ${config.mod_name} - ${config.mod_version}"
        }
    }
}

task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        System.env.MC_KS_PASS != null
    }
    keyStore = System.env.MC_KS_PATH
    alias = System.env.MC_KS_ALIAS
    storePass = System.env.MC_KS_PASS
    keyPass = System.env.MC_KS_PASS
    inputFile = jar.archiveFile
    outputFile = jar.archiveFile
}

def reobfFile = jar.archiveFile.get().asFile
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'signJar'
}

jar.finalizedBy('reobfJar')
publish.dependsOn('reobfJar')

build.dependsOn signJar

tasks.register('buildAndUploadMod') {
    group = "upload"
    dependsOn "curseforge${config.cf_project}", 'modrinth'

    doLast {
        println "Upload complete."
    }
}

afterEvaluate {
    tasks["curseforge${config.cf_project}"].dependsOn.add(publish)
    tasks["modrinth"].dependsOn.add(publish)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = config.maven_group
            artifactId = config.archive_name + "-" + config.mc_version

            artifact reobfArtifact
            artifact srcJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///SERVER//Maven"
        }
    }
}

// This piece of garbage will ensure that the build number is updated after build is executed.
tasks.build.doLast {
    if(!hasConfig("build_candidate") || !Boolean.parseBoolean(config.build_candidate))
    {
        def buildTxt = configFile.toPath();
        List<String> lines = Files.readAllLines buildTxt;
        lines.replaceAll { String ln ->
            if(ln.startsWith("mod_version"))
            {
                def version = ln.split("=")[1];
                def dot = version.lastIndexOf('.') + 1;
                def newVersion = version.substring(0, dot);
                def lastOne = Integer.parseInt(version.substring(dot)) + 1;
                return "mod_version=" + newVersion + lastOne;
            }
            return ln;
        }
        Files.write buildTxt, lines, StandardOpenOption.CREATE;
    } else println "Build candidate enabled, we are not increasing build number after this."
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
