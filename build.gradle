buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.8'
	id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.50'
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.txt"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"
group = "com.pengu.hammercore"
archivesBaseName = "HammerCore-${config.minecraft_version}"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    maven {
        name = "jei"
        url =  "http://dvs1.progwml6.com/files/maven"
    }
	maven {
		name = "HWYLA"
		url = "http://tehnut.info/maven"
	}
	maven {
		name "TheOneProbe"
		url "http://maven.tterrag.com/"
	}
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies {
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.24-B39_1.12"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.22-13"
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}:api"
    runtime "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}"
}

minecraft {
    version = "${config.forge_version}"
    runDir = "run"
    mappings = "${config.mappings}"
    makeObfSourceJar = false
    replace '@VERSION@', "${config.mod_version}"
	replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", config.minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		include 'pack.mcmeta'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
		exclude 'pack.mcmeta'
    }
	rename "hc_at.cfg", "META-INF/hc_at.cfg"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLCorePlugin': 'com.pengu.hammercore.asm.HammerCoreCore'
		attributes 'FMLCorePluginContainsFMLMod': 'false'
		attributes 'FMLAT': 'hc_at.cfg'
    }
}

task srcJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
	manifest {
        attributes 'FMLCorePlugin': 'com.pengu.hammercore.asm.HammerCoreCore'
		attributes 'FMLCorePluginContainsFMLMod': 'false'
		attributes 'FMLAT': 'hc_at.cfg'
    }
}

jar {
	manifest {
        attributes 'FMLCorePlugin': 'com.pengu.hammercore.asm.HammerCoreCore'
		attributes 'FMLCorePluginContainsFMLMod': 'false'
		attributes 'FMLAT': 'hc_at.cfg'
    }
}

artifacts {
    archives deobfJar
	archives srcJar
}

curseforge {
	apiKey = System.env.CURSEFORGE_API_KEY != null ? System.env.CURSEFORGE_API_KEY : '0'
	project {
		id = "${config.project_id}"
		changelogType = 'html'
		changelog = file('changelog.html')
		releaseType = "${config.release_type}"
		addGameVersion "${config.minecraft_version}"
		mainArtifact(jar) {
		  displayName = "[${config.minecraft_version}] ${config.mod_name} - ${config.mod_version}"
		}
		addArtifact(deobfJar) {
		  displayName = "[${config.minecraft_version}] ${config.mod_name} (Deobfuscated) - ${config.mod_version}"
		}
		addArtifact(srcJar) {
		  displayName = "[${config.minecraft_version}] ${config.mod_name} (Sources) - ${config.mod_version}"
		}
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

task signDeobfJar(type: SignJar, dependsOn: deobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = deobfJar.archivePath
    outputFile = deobfJar.archivePath
}

task signSrcJar(type: SignJar, dependsOn: srcJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = srcJar.archivePath
    outputFile = srcJar.archivePath
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("changelog.html")
    untaggedName = "Version ${project.version}"
    fromCommit = config.startGitRev
    toRef =  "HEAD"
    gitHubIssuePattern = "nonada123";
    templateContent = """
{{#tags}}
<h1>Changes:</h1>
  <ul>
    {{#commits}}
    <li> <a href="https://github.com/APengu/HammerCore/commit/{{hash}}" target=_blank> {{{message}}}</a>
</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

build.dependsOn signJar
build.dependsOn signDeobfJar
build.dependsOn signSrcJar
tasks.build.dependsOn makeChangelog

afterEvaluate {
	tasks["curseforge${config.project_id}"].dependsOn.add(signJar)
	tasks["curseforge${config.project_id}"].dependsOn.add(signDeobfJar)
	tasks["curseforge${config.project_id}"].dependsOn.add(signSrcJar)
	tasks["curseforge${config.project_id}"].dependsOn.add(makeChangelog)
}